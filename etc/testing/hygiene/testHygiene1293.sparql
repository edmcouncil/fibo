prefix owl:   <http://www.w3.org/2002/07/owl#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>

##
# banner "min 1" cardinality restrictions should be avoided because of performance considerations.

SELECT DISTINCT ?error ?class
WHERE {
  ?restriction  rdf:type owl:Restriction.
  {{?restriction owl:minQualifiedCardinality ?cardinality} UNION {?restriction owl:minCardinality ?cardinality}}.
  ?class rdfs:subClassOf ?restriction .
  FILTER (CONTAINS(str(?class), "edmcouncil"))
  FILTER (?cardinality = 1)
  BIND (concat ("ERROR: OWL class", str(?class), " is a subclass of a restriction of type owl:minCardinality or owl:minQualifiedCardinality equal to 1") AS ?error)
}