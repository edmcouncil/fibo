PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX fibo-skos: <https://spec.edmcouncil.org/fibo/vocabulary/>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT 
{
    ?vocClassIRI skos:broader ?vocClassParentIRI.

}
WHERE
{
    SELECT DISTINCT *
    {
        ?classIRI a owl:Class .
        FILTER (isIRI(?classIRI))
        FILTER (CONTAINS(str(?classIRI), 'edmcouncil')) .
        BIND (IRI(REPLACE(str(?classIRI), "/ontology/", "/vocabulary/")) as ?vocClassIRI)
        
        ?classIRI rdfs:subClassOf ?parentClassIRI 
        FILTER (isIRI(?parentClassIRI)) 
        FILTER (CONTAINS(str(?parentClassIRI), 'edmcouncil')) .
        BIND (IRI(REPLACE(str(?parentClassIRI), "/ontology/", "/vocabulary/")) as ?vocClassParentIRI)
    }
}

